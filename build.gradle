// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    apply from: 'ims-shared/deps/versions.gradle'
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath deps.gradle.tools
        classpath deps.kotlin.plugin
        classpath deps.kotlin.allopen
        classpath deps.navigation.safe_args_plugin
    }
}

allprojects {
    addRepos(repositories)
    afterEvaluate {
        tasks.withType(JavaCompile.class) {
            options.compilerArgs << "-Xmaxerrs" << "10000"
        }
        // make Dagger more performance at build time
        extensions.findByName('kapt')?.arguments {
            arg("dagger.formatGeneratedSource", "disabled")
            arg("dagger.gradle.incremental", "enabled")
        }
        if (project.plugins.hasPlugin("kotlin-kapt")) {
            kapt {
                javacOptions {
                    option("-Xmaxerrs", 10000)
                }
            }
        }
    }
    repositories {
        maven {
            name = "GithubPackages"
            url = uri("https://maven.pkg.github.com/nphau/ims-shared")
            credentials {
                username = "public_libs"
                password = "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"
            }
        }
    }
    // https://github.com/google/dagger/issues/306
    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Xmaxerrs" << "1000" // or whatever number you want
        }
    }
}
task clean(type: Delete) {
    println " --> LOOKING FOR 'build' folders to delete ..."
    delete rootProject.buildDir
    println " --> REMOVED 'build' folders completely"
}

task zipAll(type: Zip) {
    archiveBaseName = rootProject.name
    from(rootDir) {
        exclude '**/.idea/**'
        exclude '**/build/**'
        exclude '.gradle/**'
        exclude '**/*.iml'
        exclude '**/*.dll'
        exclude '**/*.so'
        exclude '**/local.properties'
    }
    destinationDirectory = file("$buildDir/dist")
}

//Skip unused gradle task
gradle.taskGraph.whenReady { graph ->
    graph.allTasks.findAll { it.name ==~ /uploadCrashlyticsMappingFileRelease.*/ }*.enabled = false
}

task copyApks(type: Copy) {
    from("$buildDir/outputs/apk") {
        exclude '**/*unsigned.apk', '**/*unaligned.apk'
    }
    into '../apks'
}